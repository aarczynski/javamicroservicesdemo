subprojects { subproject ->
    if (subproject.name ==~ /^app-.*/) {

        configurations {
            implementation
            agent
        }

        tasks.register('copyOtelAgent', Copy) {
            from configurations.agent {
                rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
            }
            into layout.buildDirectory.dir("otel-agent")
        }

        pluginManager.withPlugin('org.springframework.boot') {
            tasks.named('bootJar') {
                dependsOn 'copyOtelAgent'
            }
            tasks.named('jar') {
                enabled = false
            }
        }

        dependencies {
            implementation "io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:$otelJavaAgentVer"
            agent "io.opentelemetry.javaagent:opentelemetry-javaagent:$otelJavaAgentVer"
        }
    }
}
