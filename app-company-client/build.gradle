plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'pl.luna-software.demo.microservices'
version = '1.0.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	agent
}

tasks.register('copyOtelAgent', Copy) {
	from configurations.agent {
		rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
	}
	into layout.buildDirectory.dir("otel-agent")
}

bootJar {
	dependsOn copyOtelAgent
}

jar {
	enabled = false
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.3'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	agent "io.opentelemetry.javaagent:opentelemetry-javaagent:$otelJavaAgentVer"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.spockframework:spock-spring:2.4-M4-groovy-4.0'
	testImplementation 'org.instancio:instancio-core:5.0.2'
}

tasks.named('test') {
	useJUnitPlatform()
}
